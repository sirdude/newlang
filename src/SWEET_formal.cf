--
-- run the following command to create our compiler template:
-- bnfc -m -c SWEET.cf
--
--  This is the correct definition of the SWEET Language
--  SWEET_acutual.cf is the messy version that accepts invalid syntax
--  and automatically trys to upgrade it to sweet formal.
--  SWEET_formal only accepts # as a comment
--  SWEET_actual accepts other common comments but complains.
--

comment "#" ;

Prog.		Program ::= [Defin] ;
Pdefinf.	Defin ::= [Tm] Type Fdef ;
Pdefinv.	Defin ::= [Tm] Type Vdef ;

Fundec.		Fdef ::= Ident "(" [Decl] ")" "{" [Stm] "}" ;
Fundecarr.	Fdef ::= "*" Ident "(" [Decl] ")" "{" [Stm] "}" ;

Pdecl.		Decl ::= [Tm] Type Vdef ;

Vdec.		Vdef ::= Ident ;
Vdecp.		Vdef ::= "*" Ident ;
Vdecarr.	Vdef ::= Ident "[" Exp "]" ;

terminator Defin "" ;
terminator Stm "" ;
terminator Tm "" ;

separator nonempty Vdef "," ;
separator nonempty Decl "," ;
separator Exp "," ;

coercions Exp 4 ;

SInherit.	Stm ::= "inherit" Conststr ;
SInclude.	Stm ::= "include" Conststr ;

SDecl.		Stm ::= Decl ";" ;
SExp.		Stm ::= Exp ";" ;
SBlock.		Stm ::= "{" [Stm] "}" ;
SWhile.		Stm ::= "while" "(" Exp ")" Stm ;
SdoWhile.	Stm ::= "do" Stm "while" "(" Exp ")" ;
SReturn.	Stm ::= "return" Exp ";" ;
Sif.		Stm ::= "if" "(" Exp ")" Stm ;
SifElse.	Stm ::= "if" "(" Exp ")" Stm "else" Stm ;
Sfor.		Stm ::= "for" "(" Exp ";" Exp ";" Exp ")" Stm ;
Srlimits.	Stm ::= "rlimits" "(" Exp ";" Exp ")" Stm ;

EAss.		Exp ::= Ident "=" Exp ;
EOr.		Exp1 ::= Exp2 "||" Exp2 ;
EAnd.		Exp1 ::= Exp2 "&&" Exp2 ;
ELt.		Exp1 ::= Exp2 "<" Exp2 ;
EGt.		Exp1 ::= Exp2 ">" Exp2 ;
ELtEq.		Exp1 ::= Exp2 "<=" Exp2 ;
EGtEq.		Exp1 ::= Exp2 ">=" Exp2 ;
EEq.		Exp1 ::= Exp2 "==" Exp2 ;
ENEq.		Exp1 ::= Exp2 "!=" Exp2 ;
EPlusEq.	Exp1 ::= Exp2 "+=" Exp2 ;
EMinusEq.	Exp1 ::= Exp2 "-=" Exp2 ;
EAdd.		Exp2 ::= Exp2 "+" Exp3 ;
ESub.		Exp2 ::= Exp2 "-" Exp3 ;
EMul.		Exp3 ::= Exp3 "*" Exp4 ;
EDiv.		Exp3 ::= Exp3 "/" Exp4 ;
Call.		Exp4 ::= Ident "(" [Exp] ")" ;
EVar.		Exp4 ::= Ident ;
EStr.		Exp4 ::= String ;
EInt.		Exp4 ::= Integer ;
EDouble.	Exp4 ::= Double ;

Cstr.		Conststr ::= Conststr "+" Conststr ;
Cstrs.		Conststr ::= String ;
Cstri.		Conststr ::= Ident ;
Cstrid.		Conststr ::= Integer ;
Cstrd.		Conststr ::= Double ;
Cstrterm.	Conststr ::= ";" ;

TVoid.		Type ::= "void" ;
TInt.		Type ::= "int" ;
TDouble.	Type ::= "float" ;
TString.	Type ::= "string" ;
TObject.	Type ::= "object" ;
TMapping.	Type ::= "mapping" ;
TMixed.		Type ::= "mixed" ;

TMAtomic.	Tm ::= "atomic" ;
TMNomask.	Tm ::= "nomask" ;
TMPrivate.	Tm ::= "private" ;
TMStatic.	Tm ::= "static" ;
